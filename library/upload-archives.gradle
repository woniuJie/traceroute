apply plugin: 'maven'
apply plugin: 'signing'

// https://stackoverflow.com/questions/28630470/upload-sources-to-nexus-repository-with-gradle

def isReleaseBuild() {
    return ARTIFACT_VERSION.contains("SNAPSHOT") == false
}

signing {
    // isReleaseVersion && 只针对release包签名，需要自己依据版本号实现这个方法
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {

        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        snapshotRepository(url: MAVEN_SNAPSHOTS_URL) {
            authentication(userName: NEXUS_USER_NAME, password: NEXUS_PASSWORD)
        }

        repository(url: MAVEN_URL) {
            authentication(userName: NEXUS_USER_NAME, password: NEXUS_PASSWORD)
        }

        pom.project {
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version ARTIFACT_VERSION
            name REPOSITORY_NAME
            description REPOSITORY_DESCRIPTION
            packaging ARTIFACT_PACKAGING
        }

    }
}

//将源代码打包进aar
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
}
